apply plugin: 'com.android.application'

android {
    signingConfigs {
        main
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "de.Maxr1998.xposed.mnts"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 6
        versionName "1.1.4"
    }
    buildTypes {
        release {
            debuggable false
            signingConfig signingConfigs.main
        }
        debug {
            debuggable true
            signingConfig signingConfigs.main
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

def propFile = project.file("keyStore.properties")
if (propFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(propFile))
    android.signingConfigs.main.storeFile project.file(props.keyStore)
    android.signingConfigs.main.storePassword props.keyStorePassword
    android.signingConfigs.main.keyAlias props.keyAlias
    android.signingConfigs.main.keyPassword props.keyPassword
}

repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    provided 'de.robv.android.xposed:api:81'
    provided 'de.robv.android.xposed:api:81:sources'
    compile project(':track-selector-lib')
    compile 'com.android.support:recyclerview-v7:25.1.0'
}